apply plugin: 'com.android.application'
apply plugin: 'com.jakewharton.butterknife'
apply plugin: 'org.greenrobot.greendao'

def configurations = rootProject.ext.configurations
def libraries = rootProject.ext.dependencies

def buildTime() {
    return new Date().format("yyyy-MM-dd-HH-mm")
}

android {
    compileSdkVersion configurations.compileSdkVersion
    buildToolsVersion configurations.buildToolsVersion
    useLibrary 'org.apache.http.legacy'

    defaultConfig {
        applicationId configurations.applicationId
        minSdkVersion configurations.minSdkVersion
        targetSdkVersion configurations.targetSdkVersion
        versionCode configurations.versionCode
        versionName configurations.versionName
        multiDexEnabled true
        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
    }

    aaptOptions {
        cruncherEnabled false
    }

    dexOptions {
        // incremental true
        preDexLibraries false
        javaMaxHeapSize "4g"
    }

    lintOptions {
        abortOnError false
    }

    packagingOptions {
        exclude 'META-INF/DEPENDENCIES.txt'
        exclude 'META-INF/LICENSE.txt'
        exclude 'META-INF/NOTICE.txt'
        exclude 'META-INF/NOTICE'
        exclude 'META-INF/LICENSE'
        exclude 'META-INF/DEPENDENCIES'
        exclude 'META-INF/notice.txt'
        exclude 'META-INF/license.txt'
        exclude 'META-INF/dependencies.txt'
    }

    buildTypes {
        release {
            applicationIdSuffix '.release'
            versionNameSuffix '-release'
            minifyEnabled true
            zipAlignEnabled true
            shrinkResources true
            debuggable false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.txt'
        }

        debug {
            applicationIdSuffix '.beta'
            versionNameSuffix '-beta'
            minifyEnabled false
            zipAlignEnabled false
            shrinkResources false
            debuggable true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.txt'
        }
    }

    lintOptions {
        abortOnError false
    }

    greendao {
        schemaVersion 1
        daoPackage 'xyz.zimuju.greendao.gen'
        targetGenDir 'src/main/java'
    }

    afterEvaluate {
        tasks.matching {
            it.name.startsWith('dex')
        }.each { dx ->
            if (dx.additionalParameters == null) {
                dx.additionalParameters = ['--multi-dex']
            } else {
                dx.additionalParameters += '--multi-dex'
            }
        }
    }

    android.applicationVariants.all { variant ->
        variant.outputs.each { output ->
            if (variant.buildType.name.equals('release')) {
                def outputFile = output.outputFile
                def fileName = "YuanYuan-${defaultConfig.versionCode}-${defaultConfig.versionName}-${buildTime()}.apk"
                output.outputFile = new File(outputFile.parent, fileName)
            }
        }
    }


}

dependencies {
    compile fileTree(include: ['*.jar'], dir: 'libs')
    compile libraries.constraintLayout
    compile libraries.multidex
    testCompile libraries.junit
    // 内存泄露的检测
    debugCompile 'com.squareup.leakcanary:leakcanary-android:1.5.1'
    releaseCompile 'com.squareup.leakcanary:leakcanary-android-no-op:1.5.1'
    testCompile 'com.squareup.leakcanary:leakcanary-android-no-op:1.5.1'
    // project
    compile project(':Common')
    compile project(':QRCode')
    compile project(':Guide')
    compile project(':Dialog')
    //butterknife
    compile libraries.butterKnife
    annotationProcessor libraries.butterKnifeCompiler
    // jar file
    compile files('libs/alipaySingle-20170510.jar')
    compile files('libs/Bmob_Push_V0.8beta_20160420.jar')
    compile files('libs/weiboSDKCore_3.1.4.jar')
    compile files('libs/BmobSDK_3.5.6_20170705.jar')
    // compile 'com.mikepenz:google-material-typeface:2.2.0.1@aar'
    compile 'com.afollestad.material-dialogs:core:0.8.5.9'
    compile 'com.squareup.okhttp3:okhttp:3.2.0'
    compile 'com.github.bumptech.glide:glide:3.7.0'
    compile 'com.github.chrisbanes:PhotoView:2.1.3'
    compile 'de.hdodenhof:circleimageview:1.3.0'
    compile 'com.google.code.gson:gson:2.7'
    //RxAndroid
    compile 'io.reactivex.rxjava2:rxandroid:2.0.1'
    compile 'io.reactivex.rxjava2:rxjava:2.0.8'
    compile 'com.tbruyelle.rxpermissions2:rxpermissions:0.9.4@aar'
    compile 'com.jakewharton.rxbinding2:rxbinding:2.0.0'
    compile 'com.trello.rxlifecycle2:rxlifecycle-components:2.0.1'
    //Retrofit
    compile 'com.squareup.retrofit2:retrofit:2.1.0'
    compile 'com.squareup.retrofit2:converter-gson:2.1.0'
    compile 'com.squareup.retrofit2:adapter-rxjava2:2.2.0'
    //compile 'com.squareup.okhttp:logging-interceptor:3.6.0'
    compile 'com.umeng.analytics:analytics:latest.integration'
    compile 'com.nineoldandroids:library:2.4.0'
    //RecyclerView Adapter
    compile 'me.drakeet.multitype:multitype:2.4.3'
    compile 'com.makeramen:roundedimageview:2.3.0'
    compile 'me.solidev.library:status-view-layout:0.2.2'
    compile 'org.jsoup:jsoup:1.10.2'
    compile 'com.yqritc:recyclerview-flexibledivider:1.4.0'
    //support package
    compile 'com.android.support:recyclerview-v7:25.3.1'
    compile 'com.android.support:design:25.3.1'
    compile 'com.android.support:support-v4:25.3.1'
    compile 'com.android.support:appcompat-v7:25.3.1'
    compile 'com.android.support:cardview-v7:25.3.1'
    compile 'org.greenrobot:greendao:3.2.2'
    compile 'org.greenrobot:greendao-generator:3.2.2'
    compile files('libs/BmobSMS_V1.2.2_20170303.jar')
}