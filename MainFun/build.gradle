apply plugin: 'com.android.application'
apply plugin: 'com.jakewharton.butterknife'
apply plugin: 'org.greenrobot.greendao'

def configurations = rootProject.ext.configurations
def libraries = rootProject.ext.dependencies

def buildTime() {
    return new Date().format("yyyy-MM-dd-HH-mm")
}

android {
    compileSdkVersion configurations.compileSdkVersion
    buildToolsVersion configurations.buildToolsVersion
    useLibrary 'org.apache.http.legacy'

    defaultConfig {
        applicationId configurations.applicationId
        minSdkVersion configurations.minSdkVersion
        targetSdkVersion configurations.targetSdkVersion
        versionCode configurations.versionCode
        versionName configurations.versionName
        multiDexEnabled true
        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
    }

    // dataBinding 加入
    dataBinding {
        enabled = true
    }

    aaptOptions {
        cruncherEnabled false
    }

    dexOptions {
        preDexLibraries false
        javaMaxHeapSize "4g"
    }

    lintOptions {
        abortOnError false
    }

    packagingOptions {
        exclude 'META-INF/DEPENDENCIES.txt'
        exclude 'META-INF/LICENSE.txt'
        exclude 'META-INF/NOTICE.txt'
        exclude 'META-INF/NOTICE'
        exclude 'META-INF/LICENSE'
        exclude 'META-INF/DEPENDENCIES'
        exclude 'META-INF/notice.txt'
        exclude 'META-INF/license.txt'
        exclude 'META-INF/dependencies.txt'
        exclude 'META-INF/rxjava.properties'
    }

    buildTypes {
        release {
            applicationIdSuffix '.release'
            versionNameSuffix '-release'
            minifyEnabled true
            zipAlignEnabled true
            shrinkResources true
            debuggable false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.txt'
        }

        debug {
            applicationIdSuffix '.beta'
            versionNameSuffix '-beta'
            minifyEnabled false
            zipAlignEnabled false
            shrinkResources false
            debuggable true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.txt'
        }
    }

    lintOptions {
        abortOnError false
    }

    greendao {
        schemaVersion 1
        daoPackage 'xyz.zimuju.greendao.gen'
        targetGenDir 'src/main/java'
    }

    afterEvaluate {
        tasks.matching {
            it.name.startsWith('dex')
        }.each { dx ->
            if (dx.additionalParameters == null) {
                dx.additionalParameters = ['--multi-dex']
            } else {
                dx.additionalParameters += '--multi-dex'
            }
        }
    }

    android.applicationVariants.all { variant ->
        variant.outputs.each { output ->
            if (variant.buildType.name.equals('release')) {
                def outputFile = output.outputFile
                def fileName = "YuanYuan-${defaultConfig.versionCode}-${defaultConfig.versionName}-${buildTime()}.apk"
                output.outputFile = new File(outputFile.parent, fileName)
            }
        }
    }


}

dependencies {
    compile libraries.constraintLayout
    compile libraries.multidex
    testCompile libraries.junit

    // project
    compile project(':Common')
    compile project(':Guide')
    compile project(':Dialog')
    compile project(':QRCode')
    compile project(':Reader')

    // jar file
    compile files('libs/alipaySingle-20170510.jar')
    compile files('libs/weiboSDKCore_3.1.4.jar')
    compile files('libs/Bmob_Push_V1.0.0beta_20160627.jar')
    compile files('libs/BmobNewIM_2.0.9_20170731.jar')
    compile files('libs/BmobPay_v3.2.0_170602.jar')
    compile files('libs/BmobSDK_3.5.6_20170705.jar')
    compile files('libs/BmobSMS_V1.2.2_20170303.jar')

    // 内存泄露的检测
    debugCompile libraries.leakCanaryAndroid
    releaseCompile libraries.leakCanaryAndroidNoOp
    testCompile libraries.leakCanaryAndroidNoOp

    //butterknife
    compile libraries.butterKnife
    annotationProcessor libraries.butterKnifeCompiler

    compile libraries.googleMaterialTypeface
    compile libraries.materialDialogsCore
    compile libraries.okhttp
    compile libraries.glide
    compile libraries.photoView
    compile libraries.circleImageView
    compile libraries.gson

    //RxAndroid
    compile libraries.rxAndroid
    compile libraries.rxJava
    compile libraries.okio
    compile libraries.rxPermissions
    compile libraries.rxBinding
    compile libraries.rxLifecycleComponents

    //Retrofit
    compile libraries.retrofit
    compile libraries.converterGson
    compile libraries.adapterRxJava2

    compile libraries.umengAnalytics
    compile libraries.nineOldAndroidsLibrary

    //RecyclerView Adapter
    compile libraries.multitype
    compile libraries.roundedImageView
    compile libraries.statusViewLayout
    compile libraries.jsoup
    compile libraries.recyclerviewFlexibleDivider

    //support package
    compile libraries.v7recyclerView
    compile libraries.design
    compile libraries.v4support
    compile libraries.v7appcompat
    compile libraries.v7cardview

    // green dao
    compile libraries.greendao
    compile libraries.greendaoGenerator

    // LeanCloud 基础包
    compile libraries.avoscloudSdk
    // 推送与实时聊天需要的包
    compile libraries.avoscloudPush
    // LeanCloud 统计包
    compile libraries.avoscloudStatistics
    // LeanCloud 用户反馈包
    compile libraries.avoscloudFeedback
    // avoscloud-sns：LeanCloud 第三方登录包
    compile libraries.avoscloudSns
    // LeanCloud 应用内搜索包
    compile libraries.avoscloudSearch
}